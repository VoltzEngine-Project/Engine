buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'scala'
apply plugin: 'maven-publish'

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
group = "${System.getenv().JOB_NAME}".toLowerCase()
archivesBaseName = "${System.getenv().JOB_NAME}"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
	
	replaceIn "components/CalclaviaLoader.java"
	replace "@MAJOR@", config.version.mod.major
	replace "@MINOR@", config.version.mod.minor
	replace "@REVIS@", config.version.mod.revis
	replace "@BUILD@", System.getenv("BUILD_NUMBER")
}

if (System.getenv("BUILD_NUMBER") != null)
    version += ".${System.getenv("BUILD_NUMBER")}"


task copyBuildXml(type: Copy) {
    from 'build.properties'
    into 'output'
}

jar {
    dependsOn copyBuildXml
    classifier = 'universal'
	
	manifest {
        attributes FMLCorePlugin: "calclavia.lib.CalclaviaPluginLoader",
				   FMLCorePluginContainsFMLMod: "true"
    }
	
    destinationDir = file 'output'
}

task apiZip(type: Zip) {
    classifier = 'api'
    from sourceSets*.allSource
    include 'calclavia/api/**'
    destinationDir = file 'output'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'dev'

    manifest {
        attributes FMLCorePlugin: "calclavia.lib.CalclaviaPluginLoader",
				   FMLCorePluginContainsFMLMod: "true"
    }
}

artifacts {
    archives apiZip
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact deobfJar
			artifact apiZip

			artifact changelog{
				classifier "changelog"
				file 'output/' + "${System.getenv().JOB_NAME}-${project.version}" + '-changelog' + '.txt'
			}
			
			artifact properties{
				classifier "properties"
				file 'output/build.properties'
			}
        }
    }
    repositories {
        maven {
            url "file://var/www/maven"
        }
    }
}

repositories {
    maven { url 'http://calclavia.com/maven' }
}
dependencies {
    compile 'universalelectricity:Universal-Electricity:+:dev'
}


import net.minecraftforge.gradle.tasks.dev.ChangelogTask
import net.minecraftforge.gradle.common.Constants
import net.minecraftforge.gradle.delayed.*

task createChangelog(type: ChangelogTask) {
    def jobName = "${System.getenv().JOB_NAME}"
    def buildNumber = "${System.getenv().BUILD_NUMBER}"
    jobName.replaceAll(' ','%20')
    setServerRoot(new DelayedString(project, 'http://ci.calclavia.com/'))
    setJobName(new DelayedString(project, jobName.toString()));
    setAuthName(new DelayedString(project, 'console_script'));
    setAuthPassword(new DelayedString(project, '625d2ac53190be3422faa0c474fb299b'));
    setTargetBuild({buildNumber.toString()});
    setOutput(new DelayedFile(project, 'output/' + jobName + "-${project.version}" + '-changelog' + '.txt'));
}

build.dependsOn "createChangelog"